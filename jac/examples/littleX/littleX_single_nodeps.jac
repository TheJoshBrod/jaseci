import datetime;
cl import from jac:client_runtime {
    jacLogin,
    jacLogout,
    jacSignup,
    renderJsxTree,
}
cl import from lodash { * as _ }
cl import from antd { Button, Input, Card, Typography, Space }
cl import from pluralize { default as pluralize }

node Profile {
    has username: str = "";

    can update with update_profile entry {
        self.username = visitor.new_username;
        report self ;
    }

    can get with get_profile entry {
        follwers = [
            {"id" : jid(i) , "username" : i.username }
            for i in [self-->( ` ? Profile ) ]
        ];
        report {"user" : self , "followers" : follwers } ;
    }

    can follow with follow_request entry {
        current_profile = [root-->( ` ? Profile ) ];
        current_profile[0] +>: Follow() :+> self;
        report self ;
    }

    can un_follow with un_follow_request entry {
        
        current_profile = [root-->( ` ? Profile ) ];
        follow_edge = [edge current_profile[0]->:Follow :->self];
        del follow_edge[0] ;
        report self ;
    }
}


obj TweetInfo {
    has username: str;
    has id: str;
    has content: str;
    has embedding: list;
    has likes: list;
    has comments: list;
}


node Tweet {
    has content: str;
    has embedding: list;
    has created_at: str = datetime.datetime.now().strftime("%Y-%m-%d %H:%M:%S");

    can update with update_tweet exit {
        self.content = visitor.updated_content;
        report self ;
    }

    can delete with remove_tweet exit {
        del self ;
        disengage;
    }

    can like_tweet with like_tweet entry {
        current_profile = [root-->( ` ? Profile ) ];
        self +>: Like() :+> current_profile[0];
        report self ;
    }

    can remove_like with remove_like entry {
        current_profile = [root-->( ` ? Profile ) ];
        like_edge = [edge self->:Like :->current_profile[0]];
        del like_edge[0] ;
        report self ;
    }

    can comment with comment_tweet entry {
        current_profile = [root-->( ` ? Profile ) ];
        comment_node = current_profile[0] +>: Post() :+> Comment(
            content=visitor.content
        );
        grant(comment_node[0], level=ConnectPerm);
        self ++> comment_node[0];
        report comment_node[0] ;
    }

    def get_info()  -> TweetInfo {
        return TweetInfo(
            username=[self<-:Post :<-][0].username,
            id=jid(self),
            content=self.content,
            embedding=self.embedding,
            likes=[i.username for i in [self->:Like :->]],
            comments=[

                {"username" : [i<--( ` ? Profile ) ][0].username , "id" : jid(i) , "content" : i.content }
                for i in [self-->( ` ? Comment ) ]
            ]
        );
    }

    can get with load_feed entry {
        tweet_info = self.get_info();
        visitor.results.append({"Tweet_Info" : tweet_info });
    }
}


node Comment {
    has content: str;

    can update with update_comment entry {
        self.content = visitor.updated_content;
        report self ;
    }

    can delete with remove_comment entry {
        del self ;
        disengage;
    }
}


edge Follow {}


edge Like {}


edge Post {}


walker visit_profile {
    can visit_profile with `root entry {
        visit [-->( ` ? Profile ) ] else {
            new_profile = here ++> Profile();
            grant(new_profile[0], level=ConnectPerm);
            visit new_profile;
        }
    }
}


walker update_profile(visit_profile) {
    has new_username: str;
}


walker get_profile(visit_profile) {}


walker load_user_profiles {
    can load_profiles with `root entry {
        self.profiles: list = [];
        for each_root in allroots() {
            profile = [each_root-->( ` ? Profile ) ][0];
            self.profiles.append({"name" : profile.username , "id" : jid(profile) });
        }
    }

    can report_profiles with exit {
        report self.profiles ;
    }
}


walker follow_request {}


walker un_follow_request {}


walker create_tweet(visit_profile) {
    has content: str;

    can tweet with Profile entry {
        tweet_node = here +>: Post() :+> Tweet(content=self.content, embedding=[]);
        grant(tweet_node[0], level=ConnectPerm);
        report tweet_node ;
    }
}


walker update_tweet {
    has updated_content: str;
}


walker remove_tweet {}


walker like_tweet {}


walker remove_like {}


walker comment_tweet {
    has content: str;
}


walker update_comment {
    has updated_content: str;
}


walker remove_comment {}


walker load_feed(visit_profile) {
    has search_query: str = "";
    has results: list = [];

    can load with Profile entry {
        visit [-->( ` ? Tweet ) ];
        for user_node in [->:Follow :->( ` ? Profile ) ] {
            visit [user_node-->( ` ? Tweet ) ];
        }
    }

    can report_feed with exit {
        report self.results ;
    }
}


# Client-side UI Components (marked with 'cl' for browser execution)
# ===================================================================
 # Client-side router state - using a global object to hold state
 cl let appState: dict =
{"current_route" : "login" , "tweets" : [] , "loading" : False };


# Router helper functions
cl def navigate_to(route: str) -> None {
    console.log("Navigating to:", route);
    appState["current_route"] = route;
    window.history.pushState({}, "", "#" + route);
    # Call render_app asynchronously
    render_app();
# Fire and forget - browser will handle the async
 }


cl def render_app()  -> None {
    console.log("render_app called, route:", appState.get("current_route", "unknown"));
    root_element = document.getElementById("__jac_root");
    if root_element {
        component = App();
        console.log("Rendering component to root");
        renderJsxTree(component, root_element);
        console.log("Render complete");
    }
}


cl def get_current_route()  -> str {
    return appState.get("current_route", "login");
}


# Handle browser back/forward buttons
cl def handle_popstate(event: any) -> None {
    hash = window.location.hash;
    if hash {
        appState["current_route"] = hash[1:];
    # Remove the '#'
     } else {
        appState["current_route"] = "login";
    }
    render_app();
}


# Initialize route from URL hash
cl def init_router()  -> None {
    hash = window.location.hash;
    if hash {
        appState["current_route"] = hash[1:];
    # Remove the '#'
     } else {
        if jacIsLoggedIn() {
            appState["current_route"] = "home";
        } else {
            appState["current_route"] = "login";
        }
    }
    window.addEventListener("popstate", handle_popstate);
}


# Shared data model for client/server
cl obj ClientTweet {
    has username: str = "";
    has id: str = "";
    has content: str = "";
    has likes: list = [];
    has comments: list = [];
}


cl obj ClientProfile {
    has username: str = "";
    has id: str = "";
}


# UI Components - Render a single tweet card
cl def TweetCard(tweet: ClientTweet) -> any {
    return <div
        class="tweet-card"
        style={
        {"border" : "1px solid #e1e8ed" , "padding" : "15px" , "margin" : "10px 0" , "borderRadius" : "8px" }}>
    <div
            class="tweet-header"
            style={{"fontWeight" : "bold" , "marginBottom" : "80px" }}>
        @
        {tweet.username}
        </div>
    <div class="tweet-content" style={{"marginBottom" : "12px" }}>
        {tweet.content}
        </div>
    <div class="tweet-actions" style={{"display" : "flex" , "gap" : "15px" }}>
        <button
                onclick={like_tweet_action(tweet.id)}
                style={{"padding" : "5px 10px" , "cursor" : "pointer" }}>
            Like (
            {tweet.likes.length}
            )
            </button>
        <button style={{"padding" : "5px 10px" }}>
            Comment (
            {tweet.comments.length}
            )
            </button>
        </div>
    </div>;
}


# Handle liking a tweet - calls server walker directly
# The compiler automatically transforms this to __jacCallFunction at compile time!
 cl async def like_tweet_action(
    tweet_id: str
) -> any {
    try {
        result = await like_tweet(tweet_id);
        print("Tweet liked:", result);
        # Re-render feed after like
        window.location.reload();
    } except Exception as e {
        print("Error liking tweet:", e);
    }
}


# Render the main feed view
cl def FeedView(tweets: list) -> any {
    return <div
        class="feed-container"
        style={
        {"maxWidth" : "600px" , "margin" : "0 auto" , "fontFamily" : "sans-serif" }}>
    <div
            class="feed-header"
            style={{"padding" : "20px" , "borderBottom" : "1px solid #e1e8ed" }}>
        <h1 style={{"margin" : "0" }}>
            LittleX Feed
            </h1>
        </div>
    <div class="feed-content">
        {[TweetCard(tweet) for tweet in tweets]}
        </div>
    </div>;
}


# Render login form
cl def LoginForm()  -> any {
    suggestions = ['alice', 'bob', 'charlie', 'diana', 'eve'];
    randomSuggestion = _.sample(suggestions);
    item = 'apple';
    count = 5;
    result = pluralize(item, count, true); 

    return <div
        class="login-container"
        style={
        {"maxWidth" : "400px" , "margin" : "50px auto" , "padding" : "20px" , "border" : "1px solid #e1e8ed" , "borderRadius" : "8px" , "fontFamily" : "sans-serif" }}>
    <h2 style={{"marginTop" : "0" }}>
        Login to LittleX
        </h2>
    <div style={{"marginTop" : "15px" , "textAlign" : "center" }}>
        # Add random suggestion
        {randomSuggestion}
        </div>
        <div style={{"marginTop" : "15px" , "textAlign" : "center" }}>
            {result}
        </div>
    <form onsubmit={handle_login}>
        <div style={{"marginBottom" : "15px" }}>
            <label style={{"display" : "block" , "marginBottom" : "5px" }}>
                Username:
                </label>
            <input
                type="text"
                id="username"
                style={
                {"width" : "100%" , "padding" : "8px" , "boxSizing" : "border-box" }}/>
            </div>
        <div style={{"marginBottom" : "15px" }}>
            <label style={{"display" : "block" , "marginBottom" : "5px" }}>
                Password:
                </label>
            <input
                type="password"
                id="password"
                style={
                {"width" : "100%" , "padding" : "8px" , "boxSizing" : "border-box" }}/>
            </div>
        <button
                type="submit"
                style={
                {"width" : "100%" , "padding" : "10px" , "backgroundColor" : "#1da1f2" , "color" : "white" , "border" : "none" , "borderRadius" : "4px" , "cursor" : "pointer" }}>
            Login
            </button>
        </form>
    <div style={{"marginTop" : "15px" , "textAlign" : "center" }}>
        <a
                href="#signup"
                onclick={go_to_signup}
                style={
                {"color" : "#1da1f2" , "textDecoration" : "none" , "cursor" : "pointer" }}>
            Don't have an account? Sign up
            </a>
        </div>
    </div>;
}


# Handle login form submission
cl async def handle_login(event: any) -> None {
    event.preventDefault();
    username = document.getElementById("username").value;
    password = document.getElementById("password").value;
    success = await jacLogin(username, password);
    if success {
        navigate_to("home");
    } else {
        alert("Login failed. Please try again.");
    }
}


# Render signup form
cl def SignupForm()  -> any {
    return <div
        class="signup-container"
        style={
        {"maxWidth" : "400px" , "margin" : "50px auto" , "padding" : "20px" , "border" : "1px solid #e1e8ed" , "borderRadius" : "8px" , "fontFamily" : "sans-serif" }}>
    <h2 style={{"marginTop" : "0" }}>
        Sign Up for LittleX
        </h2>
    <form onsubmit={handle_signup}>
        <div style={{"marginBottom" : "15px" }}>
            <label style={{"display" : "block" , "marginBottom" : "5px" }}>
                Username:
                </label>
            <input
                type="text"
                id="signup-username"
                required
                style={
                {"width" : "100%" , "padding" : "8px" , "boxSizing" : "border-box" }}/>
            </div>
        <div style={{"marginBottom" : "15px" }}>
            <label style={{"display" : "block" , "marginBottom" : "5px" }}>
                Password:
                </label>
            <input
                type="password"
                id="signup-password"
                required
                style={
                {"width" : "100%" , "padding" : "8px" , "boxSizing" : "border-box" }}/>
            </div>
        <div style={{"marginBottom" : "15px" }}>
            <label style={{"display" : "block" , "marginBottom" : "5px" }}>
                Confirm Password:
                </label>
            <input
                type="password"
                id="signup-password-confirm"
                required
                style={
                {"width" : "100%" , "padding" : "8px" , "boxSizing" : "border-box" }}/>
            </div>
        <button
                type="submit"
                style={
                {"width" : "100%" , "padding" : "10px" , "backgroundColor" : "#1da1f2" , "color" : "white" , "border" : "none" , "borderRadius" : "4px" , "cursor" : "pointer" }}>
            Sign Up
            </button>
        </form>
    <div style={{"marginTop" : "15px" , "textAlign" : "center" }}>
        <a
                href="#login"
                onclick={go_to_login}
                style={
                {"color" : "#1da1f2" , "textDecoration" : "none" , "cursor" : "pointer" }}>
            Already have an account? Login
            </a>
        </div>
    </div>;
}


# Navigation helper functions
cl def go_to_login(event: any) -> None {
    event.preventDefault();
    navigate_to("login");
}


cl def go_to_signup(event: any) -> None {
    event.preventDefault();
    navigate_to("signup");
}


cl def go_to_home(event: any) -> None {
    event.preventDefault();
    navigate_to("home");
}


cl def go_to_profile(event: any) -> None {
    event.preventDefault();
    navigate_to("profile");
}


# Handle signup form submission
cl async def handle_signup(event: any) -> None {
    event.preventDefault();
    username = document.getElementById("signup-username").value;
    password = document.getElementById("signup-password").value;
    password_confirm = document.getElementById("signup-password-confirm").value;
    # Client-side validation
     if password != password_confirm {
        alert("Passwords do not match!");
        return;
    }
    if username.length < 3 {
        alert("Username must be at least 3 characters long.");
        return;
    }
    if password.length < 6 {
        alert("Password must be at least 6 characters long.");
        return;
    }
    # Use runtime auth function - no need to know about /user/create endpoint!
    result = await jacSignup(username, password);
    if result.get("success") {
        alert("Account created successfully! Welcome to LittleX!");
        navigate_to("home");
    } else {
        alert(result.get("error", "Signup failed"));
    }
}


# Handle logout
cl def logout_action()  -> None {
    jacLogout();
    navigate_to("login");
}


# Main App component that handles routing
cl def App()  -> any {
    route = get_current_route();
    nav_bar = build_nav_bar(route);
    content_view = get_view_for_route(route);
    return <div class="app-container">
    
    {nav_bar}
    {content_view}
    
    </div>;
}


# Get the view for a given route (handles async)
cl def get_view_for_route(
    route: str
) -> any {
    if route == "signup" {
        return SignupForm();
    }
    if route == "home" {
        # For home, return a loading placeholder and load async
         return HomeViewLoader();
    }
    if route == "profile" {
        return ProfileView();
    }
    # Default to login
     return LoginForm();
}


# Loader component for home view that handles async loading
cl def HomeViewLoader()  -> any {
    # Trigger async load
    load_home_view();
    # Return loading state
     return <div
        style={
        {"textAlign" : "center" , "padding" : "50px" , "fontFamily" : "sans-serif" }}>
    <h2>
        Loading feed...
        </h2>
    </div>;
}


# Async function to load and render home view
cl async def load_home_view()  -> None {
    view = await HomeView();
    root = document.getElementById("__jac_root");
    if root {
        renderJsxTree(
            <div class="app-container">
            
            {build_nav_bar("home")}
            {view}
            
            </div>,
            root
        );
    }
}


# Helper to build navigation bar
cl def build_nav_bar(route: str) -> any {
    if not jacIsLoggedIn() or route == "login" or route == "signup" {
        return None;
    }
    return <nav
        style={
        {"backgroundColor" : "#1da1f2" , "padding" : "15px" , "marginBottom" : "20px" }}>
    <div
            style={
            {"maxWidth" : "600px" , "margin" : "0 auto" , "display" : "flex" , "gap" : "20px" , "alignItems" : "center" }}>
        <a
                href="#home"
                onclick={go_to_home}
                style={
                {"color" : "white" , "textDecoration" : "none" , "fontWeight" : "bold" , "cursor" : "pointer" }}>
            Home
            </a>
        <a
                href="#profile"
                onclick={go_to_profile}
                style={
                {"color" : "white" , "textDecoration" : "none" , "fontWeight" : "bold" , "cursor" : "pointer" }}>
            Profile
            </a>
        <button
                onclick={logout_action}
                style={
                {"marginLeft" : "auto" , "padding" : "5px 15px" , "backgroundColor" : "white" , "color" : "#1da1f2" , "border" : "none" , "borderRadius" : "4px" , "cursor" : "pointer" , "fontWeight" : "bold" }}>
            Logout
            </button>
        </div>
    </nav>;
}


# Home view (async version of littlex_home)
cl async def HomeView()  -> any {
    if not jacIsLoggedIn() {
        navigate_to("login");
        return <div>
        </div>;
    }
    try {
        # Call load_feed walker directly - compiler transforms to __jacCallFunction!
        result = await load_feed();
        # Transform walker results to ClientTweet objects
        tweets = [];
        if result and result.reports and result.reports.length > 0 {
            feed_data = result.reports[0];
            for item in feed_data {
                if item.Tweet_Info {
                    tweet_info = item.Tweet_Info;
                    tweets.append(
                        ClientTweet(
                            username=tweet_info.username,
                            id=tweet_info.id,
                            content=tweet_info.content,
                            likes=tweet_info.likes,
                            comments=tweet_info.comments
                        )
                    );
                }
            }
        }
        return FeedView(tweets);
    } except Exception as e {
        return <div style={{"padding" : "20px" , "color" : "red" }}>
        Error loading feed:
        {String(e)}
        </div>;
    }
}


# Profile view
cl def ProfileView()  -> any {
    if not jacIsLoggedIn() {
        navigate_to("login");
        return <div>
        </div>;
    }
    return <div
        class="profile-container"
        style={
        {"maxWidth" : "600px" , "margin" : "20px auto" , "padding" : "20px" , "fontFamily" : "sans-serif" }}>
    <h1>
        Profile
        </h1>
    <div
            style={
            {"padding" : "15px" , "border" : "1px solid #e1e8ed" , "borderRadius" : "8px" }}>
        <p>
            Profile information will be displayed here.
            </p>
        </div>
    </div>;
}


# Main SPA entry point - this is what gets called when the page loads
cl def littlex_app()  -> any {
    init_router();
    return App();
}
